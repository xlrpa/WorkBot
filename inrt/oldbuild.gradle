import java.nio.charset.Charset

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'


File propFile = new File("E:/资料/jks/autojs-inrt/sign.properties");
Properties properties = new Properties()
if (propFile.exists()) {
    properties.load(new InputStreamReader(new FileInputStream(propFile), Charset.forName("utf-8")))
}
android {
    compileSdkVersion versions.compile
    buildToolsVersion versions.buildTool

    defaultConfig {
        applicationId "org.autojs.autoxjs.inrt"
        minSdkVersion versions.mini
        targetSdkVersion versions.target
        versionCode versions.appVersionCode
        versionName versions.appVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
//        buildConfigField "boolean","isMarket","true" // 这是有注册码的版本
        buildConfigField "boolean", "isMarket", "false"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        'resourcePackageName': applicationId,
                        "androidManifestFile": "$projectDir/src/main/AndroidManifest.xml".toString()
                ]
            }
        }
    }
    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
        encoding = 'utf-8'
    }
    signingConfigs {
        if (propFile.exists()) {
            release {
                keyAlias properties['keyAlias']
                keyPassword properties['keyPassword']
                storeFile file(properties['storeFile'])
                storePassword properties['storePassword']
            }
        }
    }
//    splits {
//
//        // Configures multiple APKs based on ABI.
//        abi {
//
//            // Enables building multiple APKs per ABI.
//            enable true
//
//            // By default all ABIs are included, so use reset() and include to specify that we only
//            // want APKs for x86 and x86_64.
//
//            // Resets the list of ABIs that Gradle should create APKs for to none.
//            reset()
//
//            // Specifies a list of ABIs that Gradle should create APKs for.
//            include 'armeabi-v7a', 'arm64-v8a' //,'x86', 'x86_64'
////            include 'arm64-v8a'
//            // Specifies that we do not want to also generate a universal APK that includes all ABIs.
//            universalApk false
//        }
//    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (propFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (propFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }
    flavorDimensions "channel"
    productFlavors {
        common {
            buildConfigField "boolean", "isMarket", "false"
            manifestPlaceholders = [appName: "inrt"]
            ndk {
                abiFilters = ["armeabi-v7a","arm64-v8a"]
            }
        }
        market {
            buildConfigField "boolean", "isMarket", "true"
            manifestPlaceholders = [appName: "AIX云控"]
        }
        template {
            manifestPlaceholders = [appName: "template"]
            packagingOptions {
                jniLibs {
                    useLegacyPackaging true
                }
                exclude '*'
            }
            ndk {
                //不打包so库
                abiFilters = [""]
            }
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['/libs']
            res.srcDirs += "src/main/res-i18n"
        }
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
        pickFirst 'lib/arm64-v8a/libc++_shared.so'
        pickFirst 'lib/arm64-v8a/libhiai.so'
        pickFirst 'lib/arm64-v8a/libhiai_ir.so'
        pickFirst 'lib/arm64-v8a/libhiai_ir_build.so'
        pickFirst 'lib/arm64-v8a/libNative.so'
        pickFirst 'lib/arm64-v8a/libpaddle_light_api_shared.so'
        pickFirst 'lib/armeabi-v7a/libc++_shared.so'
        pickFirst 'lib/armeabi-v7a/libhiai.so'
        pickFirst 'lib/armeabi-v7a/libhiai_ir.so'
        pickFirst 'lib/armeabi-v7a/libhiai_ir_build.so'
        pickFirst 'lib/armeabi-v7a/libNative.so'
        pickFirst 'lib/armeabi-v7a/libpaddle_light_api_shared.so'
    }
}

task publishMarket(group: 'build') {
    println "publish app"
    println "$rootDir"
    doLast {
        exec {
            //切换到sh文件所在的目录，执行脚本
            workingDir "E:/资料/jks/shell"
            //修改命令输出的地方，默认为控制台
            standardOutput = out
            //执行shell脚本
            if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
                executable 'cmd'
                args '/c', "node scnode.js $projectDir\\build\\outputs\\apk\\market\\release $rootDir 22"
            } else {
                commandLine 'sh', '-c', "node scnode.js $projectDir\\build\\outputs\\apk\\market\\release $rootDir 22"
            }
        }
    }
}

android.applicationVariants.all { variant ->
//    if (variant.buildType.name == 'template') {
        variant.mergeAssets.doLast {
            println("tag:test:"+variant)
            delete(fileTree(dir: variant.mergeAssets.outputDir, includes: ['models/**', 'mlkit-google-ocr-models/**']))
        }
//    }
}

def buildApkPlugin(File pluginProjectDir, boolean isDebug) {
    copy {
        def form = 'build\\outputs\\apk\\template\\'
        if (isDebug) form += "debug\\"
        else form += "release\\"
        from file(form)
        into new File(pluginProjectDir, 'src\\main\\assets')
        def fileName = "inrt-template-release-unsigned.apk"
        if (isDebug) fileName = "inrt-template-debug.apk"
        include fileName
        rename fileName, 'template.apk'
    }
    println 'app cp 完成'
}

task cp2APP(group: 'build') {
    doLast {
        copyTemplateToAPP(false)
    }
}

task cp2APPDebug(group: 'build') {
    doLast {
        copyTemplateToAPP(true)
    }
}

def copyTemplateToAPP(boolean isDebug) {
    def pluginProjectDirPath = '..\\app'
    println pluginProjectDirPath
    def pluginProjectDir = file(pluginProjectDirPath)
    if (!pluginProjectDir.exists() || !pluginProjectDir.isDirectory()) {
        println 'app 目录 not exists'
        return
    }
    println pluginProjectDir
    // buildApkPluginForAbi(pluginProjectDir, 'armeabi-v7a')
    buildApkPlugin(pluginProjectDir, isDebug)
}

tasks.whenTaskAdded { task ->
    println("=================task " + task.name + " added=================")

}

dependencies {
    implementation "androidx.activity:activity-ktx:1.5.0"
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.annotation:annotation:1.4.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.0'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.1-alpha01', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    //Glide
    implementation('com.github.bumptech.glide:glide:4.2.0', {
        exclude group: 'com.android.support'
    })
    implementation 'com.google.android.material:material:1.6.1'
    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:2.2.21"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.fanjun:keeplive:1.1.22'
    implementation 'com.dhh:websocket2:2.1.4'
    implementation 'com.github.SenhLinsh:Utils-Everywhere:3.0.0'
    testImplementation 'junit:junit:4.13.2'
    implementation project(':automator')
    implementation project(':common')
    implementation project(':autojs')
    implementation 'androidx.multidex:multidex:2.0.1'
    api fileTree(dir: '../app/libs', include: ['dx.jar', 'rhino-1.7.14-jdk7.jar'])
}
